#! /usr/bin/awk -f

###############################################################################
#                                                                             #
# ass_and_pr.awk script VERSION 0.01                                          #
# (c) William Wragg/Datapro Software Ltd. 2001                                #
# BY WILLIAM WRAGG                                                            #
# DATE 31/05/2001                                                             #
#                                                                             #
# Notes:                                                                      #
#  Call with:                                                                 #
#  nhs_sup                                                                    #
#                                                                             #
# All local variables are preceded with an underscore e.g. _local             #
# All global variables are composed only of letters, and the first letter is  #
# a capital. If the global variable is composed of two or more words, then    #
# each of these words is capitalised, not just the first one e.g. Global,     #
# GlobalVariable                                                              #
#                                                                             #
# The script takes an ascii comma seperated file from $DBPATH/raw_data called #
# ????ass.csv, and loads the data into the $DBPATH database, in the tables    #
# oh_dets and ol_dets. The program creates a temporay file                    #
# $PHABASE/tmp/ass_and_pr.tmp which is removed when the load is complete. The #
# $DBPATH/raw_data/????ass.csv file is also deleted on a successful load.     #
# This script also checks the order numbers it is loading to make sure that   #
# they have not been used before.
# This script is run from within the ass_and_pr.fs pharaoh script and will    #
# live in the $BINDIR directory.                                              #
#                                                                             #
###############################################################################

BEGIN {
   # Comma seperated file.
   FS = ","

   # Set up the command to get a list of all the files to be transfered.
   Cmd = "ls -1 " ENVIRON["DBPATH"]\
         "/raw_data/[0-9][0-9][0-9][0-9]ass.csv 2>/dev/null"

   # Test to see if files exist for transferring.
   Cmd | getline FilesToTransfer
   if (FilesToTransfer == "") {
      print ">>There are no files to import."
   }       
   close(Cmd)
 
   # Get each NHS Supplies file in turn. 
   # -- START FILE LOOP --
   while ((Cmd | getline) > 0) {
 
      # Set the Section counter to zero.
      DoneSection = 0

      # Set the temporary file and the order number input file.
      AssTemp = ENVIRON["PHABASE"] "/tmp/ass_and_pr.tmp"
      AssAsc = $1

      # -- SECTION 1 --
      # Check to see if the batch_id has beem transferred before.
      getline < AssAsc
      sql("lines 0; select count(*) from nhs_sup where batch_id = " $1 ";")
      close(AssAsc)

      if (truncate(SqlOutput[SqlOutSz - 2]) != 0 ) { 
         system("rm " AssAsc)
         print ">>Error importing from " AssAsc
         print ">>batch date already processed."
 
         # Get next file to process.
         continue
      }
      else {
         # Get the transaction number.
         sql("lines 0; select max(transaction_no) from nhs_sup;")

         DoneSection = 1
      }
      # -- END SECTION 1 --
   
      # -- SECTION 2 --
      if (DoneSection == 1) {
         # Get the Last transaction value.
         NhsTran = truncate(SqlOutput[SqlOutSz - 2])

         # If the nhs_sup table is blank then set the transaction number to 0
         if (NhsTran == "**********") { NhsTran = 0 }
   
         NoRows = 0
   
         # Create the ascii text file ready to be loaded into nhs_sup
         while ((getline < AssAsc) > 0 ) {
            # Increment transaction number.
            NhsTran++
   
            # Count The number of records in the file.
            NoRows++
   
            # Comma seperated file, so strip off quotes.
            gsub("\"","",$0)
   
            Record = NhsTran "|" $1 "|" $2 "|" $3 "|" $4\
                             "|" $5 "|" $6 "|" $7 "|" "N"
            print Record > AssTemp
         }
   
         if (NoRows > 0) { DoneSection = 2 }
      }
      # -- END SECTION 2 --
   
      if (DoneSection > 0 ) {
         close(AssTemp)
         close(AssAsc)
      }
   
      # -- SECTION 3 --
      if (DoneSection == 2) {
         # Do the insert into the nhs_sup table.
         sql("insert into nhs_sup values from '" AssTemp "';")
   
         if (SqlOutput[SqlOutSz - 2] == NoRows " row(s) added.") {
            DoneSection = 3
         }
         else {
            print ">>Error importing from " AssAsc
            print ">>inserting into nhs_sup" 
         }
      }
      # -- END SECTION 3 --
  
      # Remove the files
      if (DoneSection >= 2) { system("rm " AssTemp) }
      if (DoneSection == 3) {
         print ">>" AssAsc " successfully imported."
         print
         system("rm " AssAsc)
      }

   }
   # -- END FILE LOOP --
   
   exit
}


# FUNCTION SECTION.
# -----------------
function sql( _sql_statement,   # Arguments
   _nr, _cmd ) {   # Local variables

   carray(SqlOutput)
   SqlOutSz = 0
   _nr = 0
   _cmd = "echo \"" _sql_statement "\" | SQL 2>&1"

   # Starting SQL.
   while ( (_cmd | getline) > 0) {
      _nr++
      SqlOutput[_nr] = $0
   }

   SqlOutSz = _nr
   close(_cmd)
}

# This function creates an empty array.
function carray( _array) {   # Argumnets
   _array[1] = "create"
   darray(_array)
   return
}

# This function deletes all elements from an array.
function darray( _array,   # Arguments
   _item ) {   # Local variables

   if ( ! is_empty(_array) ) {
      for (_item in _array) { delete _array[_item] }
   }
}

# This function checks whether an array is empty or not. Returns 1 if empty and
# 0 if their is any data.
function is_empty( _array,   # Arguments
   _item ) {   # Local variables

   # If finds any items then array is not blank
   for (_item in _array) { return 0 }

   # Array is blank
   return 1
}

# This function chops off leading, and trailing, spaces from a string.
function truncate( _string ) {   # Arguments
   sub(/^ +/,"",_string)
   sub(/ *$/,"",_string)
   return _string
}
# ------------------------
# END OF FUNCTION SECTION.

END {
   close(Cmd)
   print
   print "PRESS ANY KEY TO CONTINUE ..."
   getline
}
